; Умножение A*B в ДК четвертым способом с автокоррекцией
; r0      - ДК(A)
; (r2,r1) - ДК(B)
; (r4,r3) - СЧП (Результат A*B)
; r5      - счетчик
;  r6,r7 - временные переменные

    in r0 ;A
    in r2 ;B - старшая часть множимого
    
    ;заполняем младшую часть множимого
    add 0,0,r1 
    
    add 0,0,r4 ;СЧП:=0
    add 0,0,r3
    
    add 0,9,r5 ;счетчик на 9 шагов --- трюк
    jz 0, shiftB ;сдвигаем только множимое
    
mainLoop:
    ; анализ младших бит множителя
    and r0,0xC0,r6
    xor r6,0x80,r7
    jz r7,subB ;обработчик 10 - вычитание
    xor r6,0x40,r7
    jz r7,addB ;обработчик 01 - вычитание
    jz 0,shifts
    
subB:
    not r2,r2 ;инвертируем множимое, чтобы сэкономить регистры
    not r1,r1
    
    add r3,r1,r6 ;сложили младшие байты СЧП (y) и множимого (x) +1 (в ДК!)
    add r6,1,r6  ;r6=s=x+y+1
    
    ;определяем был ли перенос? s=x+y+1
    not r6,r6     ;r6=~s
    or  r1,r3,r7  ;r7=x|y
    and r6,r7,r7  ;r7=~s(x|y)
    and r1,r3,r6  ;r6=xy
    or  r6,r7,r6  ;r6=~s(x|y)|xy --- условие переноса
    rol r6,1,r6
    and r6,1,r6   ;r6=(был перенос) ? 1 : 0

    add r3,r1,r3 ;сложили младшие байты СЧП и множимого
    add r3,1,r3  ; +1 для ДК
    add r4,r2,r4 ;сложили старшие байты СЧП и множимого
    add r4,r6,r4 ;с учетом переноса из младших байт
    
    not r2,r2 ;инвертируем множимое (восстанавливаем старое значение)
    not r1,r1
    
    jz 0,shifts

addB:
    add r3,r1,r6 ;сложили младшие байты СЧП и множимого
    
    ;определяем был ли перенос? s=x+y
    not r6,r6     ;r6=~s
    or  r1,r3,r7  ;r7=x|y
    and r6,r7,r7  ;r7=~s(x|y)
    and r1,r3,r6  ;r6=xy
    or  r6,r7,r6  ;r6=~s(x|y)|xy --- условие переноса
    rol r6,1,r6
    and r6,1,r6   ;r6=(был перенос) ? 1 : 0

    add r3,r1,r3 ;сложили младшие байты СЧП и множимого
    add r4,r2,r4 ;сложили старшие байты СЧП и множимого
    add r4,r6,r4 ;с учетом переноса из младших байт

shifts:
    ;shift A.
    rol r0,1,r0    
    and r0,0xFE,r0
    
shiftB:
    ;shift B (r2,r1) right
    and r2,0x80,r7 ; r7 - sign(СЧП)
    ror r2,1,r2 
    and r2,0x80,r6 ; r6 - перенос в младшую часть
    
    and r2,0x7F,r2
    or  r2,r7,r2   ; знак в старший разряд СЧП
    
    ror r1,1,r1
    and r1,0x7F,r1
    or r1,r6,r1    ; перенос младшего бита старшей части в старший бит младшей части множимого
    
    ; уменьшаем счетчик
    sub r5,1,r5 
    jz r5, exit
    jz 0,mainLoop

exit:
    ;little endian
    out r3 ;младший
    out r4 ;старший