; Умножение A*B в ДК первым способом с автокоррекцией
; r0 - ДК(A)
; r1 - ДК(B)
; (r3,r2) - СЧП (Результат A*B)
; r4 - счетчик

in r0 ;A
in r1 ;B

add 0,0,r2 ;СЧП:=0
add 0,0,r3

add 0,8,r4 ;счетчик на 8 шагов

rol r0,1,r0 ;трюк: сдвигаем множитель, чтобы компенсировать сдвиги основного цикла

and r0,0b10,r5
jz r5,shifts ; 00-сдвиги
jz 0, subB   ; 10-вычитание сдвиги

mainLoop:
and r0,0b11,r5
xor r5,0b10,r6
jz r6,subB ;обработчик 10 - вычитание
xor r5,0b01,r6
jz r6,addB ;обработчик 01 - вычитание
jz 0,shifts

subB:
sub r3,r1,r3
jz 0,shifts

addB:
add r3,r1,r3

shifts:
ror r0,1,r0    ; shift A. временно используем r5,r6

; shift СЧП 
and r3,0x80,r5 ; r5 - sign(СЧП)
ror r3,1,r3 
and r3,0x80,r6 ; r6 - перенос в младшую часть

and r3,0x7F,r3
or  r3,r5,r3   ; знак в старший разряд СЧП

ror r2,1,r2
and r2,0x7F,r2
or r2,r6,r2    ; перенос младшего бита старшей части в старший бит младшей части СЧП

sub r4,1,r4 ; уменьшаем счетчик
jz r4, exit
jz 0,mainLoop

exit:
out r3
out r2
