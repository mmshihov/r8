; Умножение A*B в ДК третьим способом с автокоррекцией
; r0 - ДК(A)
; r1 - ДК(B)
; (r3,r2) - СЧП (Результат A*B)
; r4 - счетчик
;  r5,r6,r7 - временные переменные

    in r0 ;A
    in r1 ;B
    
    add 0,0,r2 ;СЧП:=0
    add 0,0,r3
    
    add 0,8,r4 ;счетчик на 8 шагов
    
mainLoop:
    and r0,0xC0,r5
    xor r5,0x80,r6
    jz r6,subB ;обработчик 10 - вычитание
    xor r5,0x40,r6
    jz r6,addB ;обработчик 01 - вычитание
    jz 0,shifts

subB:
    not r1,r1    
    add r1,1,r1
    
    add r2,r1,r6 ;r6=s=x+y+1, где x --- инверсия множимого, y --- младшая часть СЧП
    
    ;определяем был ли перенос? s=x+y
    not r6,r6     ;r6=~s
    or  r1,r2,r7  ;r7=x|y
    and r6,r7,r7  ;r7=~s(x|y)
    and r1,r2,r6  ;r6=xy
    or  r6,r7,r6  ;r6=~s(x|y)|xy --- условие переноса
    rol r6,1,r6
    and r6,1,r6   ;r6=(был перенос) ? 1 : 0

    add r2,r1,r2 ;сложили младшие байты СЧП и множимого
    rol r1,1,r7
    and r7,1,r7
    sub 0,r7,r7; r7 --- старший байт (заполнен знаковым разрядом) множимого
    add r3,r7,r3 ;сложили старшие байты СЧП и множимого
    add r3,r6,r3 ;с учетом переноса из младших байт
    
    not r1,r1  ;восстановили множимое  
    add r1,1,r1
    
    jz 0,shifts

addB:
    add r2,r1,r6 ;r6=s=x+y+1, где x --- инверсия множимого, y --- младшая часть СЧП
    
    ;определяем был ли перенос? s=x+y
    not r6,r6     ;r6=~s
    or  r1,r2,r7  ;r7=x|y
    and r6,r7,r7  ;r7=~s(x|y)
    and r1,r2,r6  ;r6=xy
    or  r6,r7,r6  ;r6=~s(x|y)|xy --- условие переноса
    rol r6,1,r6
    and r6,1,r6   ;r6=(был перенос) ? 1 : 0

    add r2,r1,r2 ;сложили младшие байты СЧП и множимого
    rol r1,1,r7
    and r7,1,r7
    sub 0,r7,r7 ;r7 --- старший байт (заполнен знаковым разрядом) множимого
    add r3,r7,r3 ;сложили старшие байты СЧП и множимого
    add r3,r6,r3 ;с учетом переноса из младших байт

shifts:
    ; уменьшаем счетчик
    sub r4,1,r4 
    jz r4, exit
    
    ;shift A.
    rol r0,1,r0    
    and r0,0xFE,r0

    ; shift СЧП (r3,r2) left
    rol r2,1,r2 
    and r2,1,r6 ;r6 --- старший бит младшей части СЧП
    and r2,0xFE,r2
    
    rol r3,1,r3 
    and r3,0xFE,r3 
    or  r3,r6,r3
    
    jz 0,mainLoop

exit:
    out r2
    out r3
